{
    parserClass="org.acornlang.language.parser.AcornParser"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="Acorn"
    psiImplClassSuffix="Impl"
    psiPackage="org.acornlang.language.psi"
    psiImplPackage="org.acornlang.language.psi.impl"

    elementTypeHolderClass="org.acornlang.language.psi.AcornTypes"
    elementTypeClass="org.acornlang.language.psi.AcornElementType"
    tokenTypeClass="org.acornlang.language.psi.AcornTokenType"

    psiImplUtilClass="org.acornlang.language.psi.AcornPsiImplUtil"
    parserUtilClass="org.acornlang.language.parser.AcornParserUtil"

    extends("ConstDecl|NamedFnDecl|NamedEnumDecl|NamedStructDecl|NamedUnionDecl|NamedSpecDecl") = ContainerItem

    extends(".+Stmt") = Stmt

    extends(".+Expr|Block") = Expr
    elementType(".+BinExpr") = BinaryExpr
    elementType(".+BinOp") = BinaryOp

    tokens=[
        LPAREN="("
        RPAREN=")"
        LBRACE="{"
        RBRACE="}"
        LBRACKET="["
        RBRACKET="]"
        MINUS="-"
        PLUS="+"
        STAR="*"
        SLASH="/"
        EQ="="
        EQEQ="=="
        BANG="!"
        BANGEQ="!="
        LT="<"
        LTEQ="<="
        GT=">"
        GTEQ=">="
        AMP="&"
        AMPAMP="&&"
        BARBAR="||"
        SEMI=";"
        COLON=":"
        COMMA=","
        DOT="."
        AT="@"

        BREAK="break"
        CONST="const"
        CONTINUE="continue"
        ELSE="else"
        ENUM="enum"
        FALSE="false"
        FN="fn"
        FOREIGN="foreign"
        IF="if"
        LET="let"
        MUT="mut"
        RETURN="return"
        SPEC="spec"
        STRUCT="struct"
        TRUE="true"
        UNION="union"
        WHILE="while"

        INTRINSIC_IDENT="regexp:[a-zA-Z_][a-zA-Z0-9_]*!"
        IDENT="regexp:[a-zA-Z_][a-zA-Z0-9_]*"
        NUMBER="regexp:[0-9]+"
        STRING="regexp:\"[^[\"\n]]*\""

        WHITE_SPACE="regexp:[\s\t\n]+"
        DOC_COMMENT="regexp:///[^\n]*\n"
        LINE_COMMENT="regexp://[^\n]*\n"
    ]
}

//todo comments and doc comments

File ::= ContainerItem* <<eof>>;


// SECTION: Container Items
// ========================

// todo ContainerItem should have an extends rule i guess
ContainerItem ::= ConstDecl
                | NamedFnDecl
                | NamedEnumDecl
                | NamedStructDecl
                | NamedUnionDecl
                | NamedSpecDecl { recoverWhile=ContainerItem_recover }
private ContainerItem_recover ::= !(CONST | FN | ENUM | STRUCT | UNION | SPEC)

ConstDecl ::= CONST IDENT (COLON LimitedExpr)? EQ Expr SEMI { pin=1 }

// Function

NamedFnDecl ::= FN IDENT FnProto (Block | SEMI) { pin=1 }

private FnProto ::= FnParamList LimitedExpr

//todo FnParamList could be private i think
// does FnParamList need to pin/recover?
FnParamList ::= LPAREN (FnParam (COMMA (FnParam | &RPAREN))*)? RPAREN { pin(".*")=1 }
FnParam ::= IDENT (COLON LimitedExpr)? { pin=1 recoverWhile=FnParam_recover }
private FnParam_recover ::= !(COMMA | RPAREN)

// Enum

NamedEnumDecl ::= ENUM IDENT EnumBody { pin=1 }

private EnumBody ::= LBRACE EnumCaseList? RBRACE
EnumCaseList ::= EnumCase (COMMA (EnumCase | &RBRACE))*
EnumCase ::= IDENT

// Struct

NamedStructDecl ::= STRUCT IDENT StructBody { pin=1 }

private StructBody ::= LBRACE StructFieldList? RBRACE
StructFieldList ::= StructField (COMMA (StructField | &RBRACE))*

StructField ::= IDENT COLON LimitedExpr { pin=1 recoverWhile=StructField_recover }
private StructField_recover ::= !(COMMA | RBRACE) //todo this recovery rule needs to consider that the thing after might be a container item, eg include const/struct/enum/etc here.

// Union

NamedUnionDecl ::= UNION IDENT UnionBody { pin=1 }

private UnionBody ::= LBRACE UnionFieldList? RBRACE
UnionFieldList ::= UnionField (COMMA (UnionField | &RBRACE))*

UnionField ::= IDENT COLON LimitedExpr { pin=1 recoverWhile=UnionField_recover }
private UnionField_recover ::= !(COMMA | RBRACE)

// Spec

NamedSpecDecl ::= SPEC IDENT SpecBody { pin=1 }

private SpecBody ::= LBRACE NamedFnDecl* RBRACE


// SECTION: Statements
// ===================

Stmt ::= ReturnStmt
       | BreakStmt
       | ContinueStmt

       | VarDeclStmt
       | ExtendedExpr
       { recoverWhile=Stmt_recover }
//todo this is _fine_, but a much better recovery would include all the starting symbols for stmts and exprs. This version does not handle a missing semicolon well
private Stmt_recover ::= !(SEMI | RBRACE)

ReturnStmt ::= RETURN ExtendedExpr? { pin=1 }
BreakStmt ::= BREAK
ContinueStmt ::= CONTINUE

VarDeclStmt ::= LET MUT? IDENT (COLON LimitedExpr)? (EQ ExtendedExpr)? { pin=1 }


// SECTION: Expressions
// ====================

Expr ::= WhileExpr
       | IfExpr
       | Block

       | AssignBinExpr
       | TypeUnionExpr
       | LogBinExpr
       | CmpBinExpr
       | MulBinExpr
       | AddBinExpr
       | SelectExpr

       | PrimaryExpr
//todo recovery

//private ExtendedExpr ::= <<setExprMode 'ExprMode.EXTENDED' Expr>> {
//    elementType = Expr
//    name = "expr"
//}
//private LimitedExpr ::= <<setExprMode 'ExprMode.LIMITED' Expr>> {
//    elementType = Expr
//    name = "expr"
////}
//
private ExtendedExpr ::= <<setExprMode 'ExprMode.EXTENDED'>> Expr
private LimitedExpr ::= <<setExprMode 'ExprMode.LIMITED'>> Expr
//private ExtendedExpr ::= <<setExprMode 'ExprMode.EXTENDED' Expr>> {
//    elementType = Expr
//    name = "expr"
//}
//private LimitedExpr ::= <<setExprMode 'ExprMode.LIMITED' Expr>> {
//    elementType = Expr
//    name = "expr"
//}

WhileExpr ::= WHILE
IfExpr ::= IF
Block ::= LBRACE (Stmt (SEMI (Stmt | &RBRACE))*)? RBRACE

// Binary Expressions

fake BinaryExpr ::= Expr BinaryOp Expr {
    methods=[
        lhs="/Expr[0]"
        rhs="/Expr[1]"
    ]
}
fake BinaryOp ::= EQ | AMP | DOT
                | AMPAMP | BARBAR
                | EQEQ | BANGEQ | LT | LTEQ | GT | GTEQ
                | STAR | SLASH | PLUS | MINUS

//todo assign is extended only, otherwise something like `const a: a = 5;` would parse as `a = 5` as the type.
AssignBinExpr ::= Expr EQ Expr

TypeUnionExpr ::= Expr AMP Expr


LogBinExpr ::= Expr LogBinOp Expr
LogBinOp ::= AMPAMP | BARBAR

CmpBinExpr ::= Expr CmpBinOp Expr
CmpBinOp ::= EQEQ | BANGEQ | LT | LTEQ | GT | GTEQ

MulBinExpr ::= Expr MulBinOp Expr
MulBinOp ::= STAR | SLASH

AddBinExpr ::= Expr AddBinOp Expr
AddBinOp ::= PLUS | MINUS

SelectExpr ::= Expr DOT IDENT

// Primary expressions

// prefix: Minus, Bang, Amp

private PrimaryExpr ::= LiteralExpr
              | ConstructExpr
              | VarRefExpr
              | IntrinsicRefExpr

              | ParenOrTupleExpr
              | ArrayExpr
              | IndexExpr
              | CallExpr

LiteralExpr ::= NUMBER | STRING | TRUE | FALSE
VarRefExpr ::= IDENT
IntrinsicRefExpr ::= INTRINSIC_IDENT

ParenOrTupleExpr ::= LPAREN ExtendedExpr (TupleExpr_upper | RPAREN) { pin=2 elementType="ParenExpr" }
upper TupleExpr_upper ::= COMMA (ExtendedExpr (COMMA (ExtendedExpr | &RPAREN))*)? RPAREN { pin=1 elementType="TupleExpr" }

ArrayExpr ::= LBRACKET (ExtendedExpr (COMMA (ExtendedExpr | &RBRACKET))*)? RBRACKET { pin=1 }

IndexExpr ::= Expr LBRACKET ExtendedExpr RBRACKET { pin=2 }

//todo only in some cases
ConstructExpr ::= <<checkExtendedMode>> LimitedExpr LBRACE ConstructArgList RBRACE { pin=3 }
private ConstructArgList ::= (ConstructArg (COMMA (ConstructArg | &RBRACE))*)? { pin(".*")=1 }
ConstructArg ::= IDENT COLON ExtendedExpr { pin=1 }

CallExpr ::= Expr LPAREN CallArgList RPAREN { pin=2 }
private CallArgList ::= (ExtendedExpr (COMMA (ExtendedExpr | &RBRACE))*)? { pin(".*")=1 }
